import pandas as pd

# list of ids of cells to be simulated
cells = pd.read_csv(config['cellsfile']).id.values

rule all:
  input: 
    expand('spatial_plots/profile_{cell_id}.png',
           cell_id=cells
    )

rule plot:
  output:
    'spatial_plots/profile_{cell_id}.png'
  input:
    expand('spatial_data/profile_{{cell_id}}_{dendstate}.csv', 
           dendstate=['active', 'passive']
    )
  params:
    dpi=300
  script: 'scripts/plot_act_vs_pass.py'

rule merge_spatial_profiles:
  output: 
    'spatial_data/profile_{cell_id}_{dendstate}.csv'
  input: 
    expand('spatial_data/{{cell_id}}/{{dendstate}}/{rel_stim_intensity}.csv',
           rel_stim_intensity=[0.2, 1, 5, 25, 125]
    )
  script: 'scripts/merge_spatial_profile_data.py'

rule simulate_spatial_profile:
  output: 'spatial_data/{cell_id}/{dendstate}/{rel_stim_intensity}.csv'
  input: 'thresh_intensity/{cell_id}.npy'
  script: 'scripts/simulate_spatial_profile.py'

rule find_threshold_stim_intensity:
  output: 'thresh_intensity/{cell_id}.npy'
  script: 'scripts/find_thresh_intensity.py'
